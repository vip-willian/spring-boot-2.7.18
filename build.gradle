plugins {
	id "base"
	id "org.jetbrains.kotlin.jvm" apply false // https://youtrack.jetbrains.com/issue/KT-30276
	id "io.spring.nohttp" version "0.0.11"
}

description = "Spring Boot Build"

defaultTasks 'build'

nohttp {
	allowlistFile = project.file("src/nohttp/allowlist.lines")
	source.exclude "**/bin/**"
	source.exclude "**/build/**"
	source.exclude "**/out/**"
	source.exclude "**/target/**"
	source.exclude "**/.settings/**"
	source.exclude "**/.classpath"
	source.exclude "**/.project"
	source.exclude "spring-boot-project/spring-boot-tools/spring-boot-buildpack-platform/src/test/resources/org/springframework/boot/buildpack/platform/docker/export.tar"
}

check {
	dependsOn checkstyleNohttp
}

allprojects {
	group "org.springframework.boot"

	repositories {
		// 阿里云
		def ALIYUN_REPOSITORY_URL = 'https://maven.aliyun.com/repository/public/'
    	def ALIYUN_JCENTER_URL = 'https://maven.aliyun.com/repository/jcenter/'
    	def ALIYUN_GOOGLE_URL = 'https://maven.aliyun.com/repository/google/'
    	def ALIYUN_GRADLE_PLUGIN_URL = 'https://maven.aliyun.com/repository/gradle-plugin/'
    	maven { url ALIYUN_REPOSITORY_URL }
    	maven { url ALIYUN_JCENTER_URL }
    	maven { url ALIYUN_GOOGLE_URL }
    	maven { url ALIYUN_GRADLE_PLUGIN_URL }
		mavenCentral()
		if (version.contains('-')) {
			maven { url "https://repo.spring.io/milestone" }
		}
		if (version.endsWith('-SNAPSHOT')) {
			maven { url "https://repo.spring.io/snapshot" }
		}
	}

	configurations.all {
		resolutionStrategy.cacheChangingModulesFor 0, "minutes"
	}
}

tasks.named("checkstyleNohttp").configure {
	maxHeapSize = "1536m"
}
